// ============================================
// AUDIO VISUALIZER VJ - Architecture modulaire
// Version 2.0
// ============================================

import oscP5.*;
import netP5.*;

// Core components
OscP5 oscP5;
AudioManager audio;
VisualizationEngine viz;
ControlsManager controls;
HUDWindow hud;

// Background image
PImage bgImage;
boolean bgLoaded = false;
boolean showBackground = true;

// Interface pour le HUD
interface IAudioProvider {
  AudioManager getAudio();
}

// ============================================
// IMPLEMENTATION DE L'INTERFACE
// ============================================
IAudioProvider getAudioProvider() {
  return new IAudioProvider() {
    public AudioManager getAudio() {
      return audio;
    }
  };
}

void setup() {
  size(1280, 720, P3D);
  surface.setLocation(150, 200);
  frameRate(60);
  smooth(8);
  
  println("|===AUDIO VISUALIZER VJ v2.0===|");
  
  // DEBUG : Vérifier le chemin
  println("=== DEBUG IMAGE ===");
  println("Chemin data : " + dataPath(""));
  println("Chemin BG.png : " + dataPath("BG.png"));
  
  File f = new File(dataPath("BG.png"));
  println("Fichier existe ? " + f.exists());
  
  if (f.exists()) {
    println("Taille fichier : " + f.length() + " bytes");
  }
  
  // Load background image
  loadBackgroundImage();

  // Initialize OSC
  oscP5 = new OscP5(this, 12000);
  println("OSC listening on port 12000");
  
  // Initialize managers
  audio = new AudioManager(64);
  viz = new VisualizationEngine();
  controls = new ControlsManager();
  
  // HUD - passe l'interface
  hud = new HUDWindow(getAudioProvider());
  
  println("\nREADY!\n");
  controls.printHelp();
  
  // Premier update du HUD
  delay(1000);
  hud.updateInfo(viz.getCurrentModeName(), controls.paletteManager.getCurrent().name);
}
void loadBackgroundImage() {
  File f = new File(dataPath("BG.png"));
  if (f.exists()) {
    try {
      PImage temp = loadImage(dataPath("BG.png"));
      if (temp != null) {
        bgImage = createImage(width, height, ARGB);
        bgImage.copy(temp, 0, 0, temp.width, temp.height, 0, 0, width, height);
        bgLoaded = true;
        println("Image chargee : BG.png");
      } else {
        println("Erreur : Impossible de charger BG.png");
        bgLoaded = false;
      }
    } catch (Exception e) {
      println("Erreur : " + e.getMessage());
      bgLoaded = false;
    }
  } else {
    println("BG.png introuvable dans le dossier data");
    bgLoaded = false;
  }
}

void loadCustomImage(String path) {
  try {
    PImage temp = loadImage(path);
    if (temp != null) {
      bgImage = createImage(width, height, ARGB);
      bgImage.copy(temp, 0, 0, temp.width, temp.height, 0, 0, width, height);
      bgLoaded = true;
      println("Image chargee : " + path);
    } else {
      println("Erreur : Impossible de charger " + path);
      bgLoaded = false;
    }
  } catch (Exception e) {
    println("Erreur : " + e.getMessage());
    bgLoaded = false;
  }
}


void draw() {
  // ============================================
  // CALQUE 1 : FOND + DATAMOSH
  // ============================================
  
  if (bgLoaded && showBackground) {
    // Appliquer le datamosh sur l'image de fond
    if (controls.datamoshEnabled) {
      viz.datamoshEffect.apply(bgImage, audio.getBass(), audio.getMid(), audio.getTreble(), null );
    } else {
      // Si pas de datamosh, afficher l'image normale
      image(bgImage, 0, 0);
    }
    
    // Overlay semi-transparent pour atténuer le fond
    fill(0, 150);
    rect(0, 0, width, height);
    
  } else {
    background(0);
  }
  
  // ============================================
  // CALQUE 2 : EFFETS VISUELS (PAR-DESSUS)
  // ============================================
  
  // Update audio
  audio.update();
  
  // Dessiner les visualisations PAR-DESSUS le fond datamoshé
  viz.update(
    audio.getBass(), 
    audio.getMid(), 
    audio.getTreble(), 
    audio.getSpectrum(),
    controls
  );
  
  // ============================================
  // CALQUE 3 : UI (TOUJOURS AU-DESSUS)
  // ============================================
  
  if (controls.showHelp) {
    controls.drawHelpOverlay();
  }
  
  if (frameCount % 120 == 0) {
    hud.updateInfo(viz.getCurrentModeName(), controls.paletteManager.getCurrent().name);
  }
  
  if (!controls.showHelp) {
    drawMainInfo();
  }
}

void drawMainInfo() {
  pushStyle();
  
  fill(0, 150);
  noStroke();
  rect(0, height - 50, width, 50);
  
  fill(255);
  textAlign(LEFT, CENTER);
  textSize(14);
  text("Mode: " + viz.getCurrentModeName() + " | Palette: " + controls.paletteManager.getCurrent().name, 10, height - 25);
  
  textAlign(RIGHT, CENTER);
  text("F: Help | H: HUD | P/O: Palette | D: Datamosh | I: Load image", width - 10, height - 25);
  
  popStyle();
}

// ============================================
// OSC
// ============================================
void oscEvent(OscMessage msg) {
  audio.handleOSC(msg);
}

// ============================================
// CONTROLS
// ============================================
void keyPressed() {
// DATAMOSH (touche G)
  if (key == 'g' || key == 'G') {
    controls.datamoshEnabled = !controls.datamoshEnabled;
    println("Datamosh : " + (controls.datamoshEnabled ? "ON" : "OFF"));
  }
  
  // CHARGER IMAGE (touche I)
  if (key == 'i' || key == 'I') {
    selectImageFile();
  }
  
  // TOGGLE AFFICHAGE IMAGE (touche B)
  if (key == 'b' || key == 'B') {
    showBackground = !showBackground;
    println("Fond : " + (showBackground ? "ON" : "OFF"));
  }
}


void selectImageFile() {
  selectInput("Choisir une image de fond", "imageSelected");
}

void imageSelected(File selection) {
  if (selection != null) {
    loadCustomImage(selection.getAbsolutePath());
  }
}

 