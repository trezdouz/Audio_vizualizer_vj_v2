// ============================================
// AUDIO VISUALIZER VJ - Architecture modulaire
// Version 2.0
// ============================================

import oscP5.*;
import netP5.*;

// Core components
OscP5 oscP5;
AudioManager audio;
VisualizationEngine viz;
ControlsManager controls;
HUDWindow hud;

// Background image
PImage bgImage;
boolean bgLoaded = false;
boolean showBackground = true;

// Interface pour le HUD
interface IAudioProvider {
  AudioManager getAudio();
}

// ============================================
// IMPLEMENTATION DE L'INTERFACE
// ============================================


void setup() {
  size(1280, 720, P3D);
  surface.setLocation(150, 200);
  frameRate(60);
  smooth(8);
  
  println("|===AUDIO VISUALIZER VJ v2.0===|");
  
  // Load background image
  loadBackgroundImage();

  // Initialize OSC
  oscP5 = new OscP5(this, 12000);
  println("OSC listening on port 12000");
  
  // Initialize managers
  audio = new AudioManager(64);
  viz = new VisualizationEngine();
  controls = new ControlsManager();
  
  // HUD - passe l'interface
  hud = new HUDWindow(getAudioProvider());
  
  println("\nREADY!\n");
  controls.printHelp();
  
  // Premier update du HUD
  delay(1000);
  hud.updateInfo(viz.getCurrentModeName(), controls.paletteManager.getCurrent().name);
}

void loadBackgroundImage() {
  File f = new File(dataPath("BG.png"));
  if (f.exists()) {
    try {
      bgImage = loadImage(dataPath("BG.png"));
      bgImage.resize(width, height);
      bgLoaded = true;
       if (viz != null && viz.datamoshEffect != null) {
        viz.datamoshEffect.setSourceImage(bgImage);
      }
      println("Background image loaded: BG.png");
    } catch (Exception e) {
      println("Error loading BG.png: " + e.getMessage());
      bgLoaded = false;
    }
  } else {
    println("BG.png not found in data folder");
    bgLoaded = false;
  }
}

void draw() {
  // Background
  background(0);
  if (bgLoaded && controls.showBackground) {
    image(bgImage, 0, 0);
    
  } 
  
  // Update audio
  audio.update();
  
  // Update visualization
  viz.update(
    audio.getBass(), 
    audio.getMid(), 
    audio.getTreble(), 
    audio.getSpectrum(),
    controls
  );
  
  // Datamosh via viz.datamoshEffect
  if (controls.datamoshEnabled) {
  viz.datamoshEffect.apply(audio.getBass(), audio.getMid(), audio.getTreble());
  }
  
  if (controls.showHelp) {
    controls.drawHelpOverlay();
  }
  
  if (frameCount % 120 == 0) {
    hud.updateInfo(viz.getCurrentModeName(), controls.paletteManager.getCurrent().name);
  }
  

  
  // Update HUD info moins souvent (toutes les 2 secondes)
  if (frameCount % 120 == 0) {
    hud.updateInfo(
      viz.getCurrentModeName(),
      controls.paletteManager.getCurrent().name
    );
  }
  
  // Info en bas
  if (!controls.showHelp) {
    drawMainInfo();
  }
}

void drawMainInfo() {
  pushStyle();
  
  fill(0, 150);
  noStroke();
  rect(0, height - 50, width, 50);
  
  fill(255);
  textAlign(LEFT, CENTER);
  textSize(14);
  text("Mode: " + viz.getCurrentModeName() + " | Palette: " + controls.paletteManager.getCurrent().name, 10, height - 25);
  
  textAlign(RIGHT, CENTER);
  text("F: Help | H: HUD | P/O: Palette | D: Datamosh | I: Load image", width - 10, height - 25);
  
  popStyle();
}

// ============================================
// OSC
// ============================================
void oscEvent(OscMessage msg) {
  audio.handleOSC(msg);
}

// ============================================
// CONTROLS
// ============================================
void keyPressed() {
  // Touche I pour charger image
  if (key == 'i' || key == 'I') {
    selectInput("Select background image:", "imageSelected");
  } else {
    controls.handleKey(key, keyCode, viz, audio, hud);
  }
}

void imageSelected(File selection) {
  if (selection != null) {
    try {
      bgImage = loadImage(selection.getAbsolutePath());
      bgImage.resize(width, height);
      bgLoaded = true;
      
      // Passer la nouvelle image au datamosh
      viz.datamoshEffect.setSourceImage(bgImage);
      
      println("Background image loaded: " + selection.getName());
    } catch (Exception e) {
      println("Error loading image: " + e.getMessage());
    }
  }
}
